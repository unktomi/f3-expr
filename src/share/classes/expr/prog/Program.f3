package expr.prog;
import expr.*;
import f3.media.scene.*;
import f3.util.*;
import f3.util.Observer.*;
import f3.util.Observable.*;
import Expression.Canvas;
import Expression.ExpressionView;
import Expression.LinkedView;
import Expression.LinkView;
import Expression.Member;
import Expression.MemberExpression;
import Expression.ExprScene;
import Expression.ExprNode;
import Expression.GetVarView;
import expr.Context;
import expr.Context.*;
import expr.EventLayer;

public class EvalContext {
    public const eventContext is Context.MemberContext;
    public const varContext is Context.ClosureVarContext;
    public var interactive is function from () to Boolean;
    public const targetNode is Object;
    public const debugger is ObservableDebugger = DebuggerEngine {
        override public function step of a from (location is Object, x is a, k is Observer of a) to ()
        {
            if (interactive()) {
                k.onNext(x);
            }
        }
    }
    public const instanceSpace is Group;
    public const targetTimeline is Timeline;
    const lookupVar is function from String to Object;
    const lookupSpatial is function from String to Node;
    const lookupTemporal is function from String to TimeNode;

    function lookupVarDebug from (name is String) to Object {
        const r = lookupVar(name);
        println("lookup var {name} => {r}");
        return r;
    }

    function lookupSpatialDebug from (name is String) to Node {
        const r = lookupSpatial(name);
        println("lookup spatial {name} => {r}");
        return r;
    }

    function lookupTemporalDebug from (name is String) to TimeNode {
        const r = lookupTemporal(name);
        println("lookup temporal {name} => {r}");
        return r;
    }
    
    public var evaluator = bind
        new org.f3.tools.script.ScriptShell(java.lang.Thread.currentThread().getContextClassLoader());

    public function evaluate from (prog is Program) to ()
    {
        evaluate(prog, prog.debugger)
    }

    public function evaluate from (prog is Program, externalDebugger is ObservableDebugger) to ()
    {
        if (prog.sourceCode == "") {
            unsubscribe();
            return;
        }
        if (prog.parent <> null) {
            return;
        }
        const debugger = if externalDebugger <> null then externalDebugger else this.debugger;
        const f = evaluator.compileAndRun(prog.getSourceCode()) 
            as function from (ObservableDebugger, Object, 
                              function from String to Object, function from String to Node, function from String to TimeNode) to (Observable of ?)[];
        unsubscribe();
        println("evaluating with debugger: {debugger}");
        const obs = f(debugger, targetNode, lookupVarDebug, lookupSpatialDebug, lookupTemporalDebug);
        for (o in obs) {
            subscribe(o, prog.sourceCode);
        }
    }

    public function unsubscribe to () 
    {
	for (x in subs) x.dispose();
        subs = [];
    }

    var subs is Disposable[];

    public function subscribe of a from (obsvb is Observable of a, src is String) to ()
    {
	println("SUBSCRIBE {obsvb} / {src}");
        var count = 0;
        const rs = foreach (x in eventContext.content) {
            if (x is Context.ResourceNodeContext) {
                (x as Context.ResourceNodeContext).resource;
            } else null;
        }
        insert obsvb.subscribe(DefaultObserver of a {
                override function onNext from (x is a) to ()
                {
                    println("ON NEXT...{x} interactive={interactive()}");
                    if (interactive()) {
                        for (r in rs) {
                            var n = r.instance(r.id, lookupVar, lookupSpatial, lookupTemporal);
                            println("INSERTING {n} into {instanceSpace} at {targetTimeline.playhead}");
                            insert EventLayer.EventNode {
                                timestamp: targetTimeline.playhead;
                                event: n;
                            } into instanceSpace.content;
                        }
                        count++;
                        println("on next {src} {x}");
                    }
                }
            }) into subs;
    }
}

class ProgramLocation {
    public const sourceLocation is String;
    public const program is Program;
}

class ProgramDebugger is DebuggerEngine {

    const program is Program;

    override public function step of a from (location is Object, x is a, k is Observer of a) to ()
    {
        const progLoc = location as ProgramLocation;
        println("stepping program { program.event} {progLoc.sourceLocation} {x} canvas={program.canvas}");
        if (program.canvas <> null) {
            program.canvas.debuggerEngine.step(progLoc.sourceLocation, x, k);
        } else {
            k.onNext(x)
        }
    }

    override public function debug of a from (xs is Observable of a, id is Object) to Observable of a
    {
        println("compiling program: {program.event} {xs} {id}");
        super.debug(xs, ProgramLocation { sourceLocation: id.toString(), program: program});
    }
}

public class Program {

    public var event is Member;
    public var canvas is Canvas;
    public var code is ExprScene;
    public var sourceCode is String;
    public var targetPath is String;

    public var parent is Program;

    public const debugger is ProgramDebugger = ProgramDebugger {
        program: this;
    }

    public const evalContext is EvalContext;

    public const varSources = new java.util.HashMap of (Context.ClosureVarContext, Program);

    public function undefineVar from (v is Context.ClosureVarContext) to ()
    {
        varSources.remove(v);
    }

    public function defineVar from (v is Context.ClosureVarContext, p is Program) to ()
    {
        varSources.put(v, p);
    }

    public function defineVar from (v is Context.ClosureVarContext, source is String) to ()
    {
        sourceCode = source;
        parent.defineVar(v, this);
    }

    public function evaluate to () 
    {
        evalContext.evaluate(this, canvas.debuggerEngine)
    }

    public function evaluate from (sourceTemplate is String) to () 
    {
        this.sourceCode = sourceTemplate;
        evaluate();
    }

    function getSourceCode to String 
    {
        var varSource is String = "";
        for (v in varSources.values()) {
            varSource = "{varSource}{v.sourceCode}";
        }
        const result = sourceCode.replace("%VARS%", varSource);
        println("SOURCE => {result}");
        return result;
    }

    public function reset to ()
    {
        evalContext.unsubscribe();
    }

    public function deserialize to () 
    {
        const idMap = new java.util.HashMap of (String, ExprNode);
        for (v in code.views) {
            idMap.put(v.id, v);
        }
        canvas.deserializationMap.clear();
        const viewMap = new java.util.HashMap of (String, ExpressionView);
        for (v in code.views) {
            const view = v.createView(canvas);
            println("deserialized {v} into {view}");
        }
        canvas.installLinks();
    }
}