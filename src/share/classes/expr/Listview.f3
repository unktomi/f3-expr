package expr;
import f3.media.scene.*;
import f3.media.scene.Shape2D.*;
import f3.media.input.*;
import java.lang.Math;
import f3.util.*;

public abstract class Listmodel of a {
    public var size is Integer;
    public abstract function getValue from (i is Integer) to a;
}

public abstract class Element of a {
    public var value is java.lang.ref.WeakReference of a;
    public var index is Integer;
    public readonly var view is Node = bind render();
    public abstract bound function render from () to Node;
}

public abstract class Listview of a is ResizableNode, Focusable, KeyboardInputHandler, Scrollable {
    public var background is Paint = 
	ColorPaint(Color.color(.6, .6, .6, 1));
    public var selectionBackground is Paint = 
	ColorPaint(Color.color(.4, .4, .4, 1));
    public var selectionForeground is Paint = bind if focused then
	ColorPaint(Color.color(.9, .9, .9, 1)) else ColorPaint(Color.color(.77, .77, .77, 1));
    public var foreground is Paint = 
	ColorPaint(Color.color(.2, .2 , .2));

    public var model is Listmodel of a;

    public var cellHeight is Number = 16;
    
    protected bound function isSelected from (index is Integer) to Boolean 
    {
        selectedRow == index;
    }

    public var selectedRow = 0 on replace {
        const lastVisible = scrollOffset + visibleRows;
        if (selectedRow > lastVisible) {
            scrollOffset = Math.min(model.size, scrollOffset + visibleRows / 2);
        } else if (selectedRow < scrollOffset) {
            scrollOffset = selectedRow;
        }
    }

    var scrollOffset is Integer;
    var values is a[];
    bound function render1 from (e is Element of a) to Node {
        render(e.index, e.value.get());
    }

    const elementMap is java.util.Map of (a, Element of a) = new java.util.HashMap of (a, Element of a);

    function makeElement from (v is a) to Element of a 
    {
        var result = elementMap.get(v);
        if (result == null) {
            result = Element of a {
                value: new java.lang.ref.WeakReference of a(v);
                override bound function render from () to Node
                {
                    render1(this);
                }
            }
            elementMap.put(v, result);
        }
        return result;
    };
    readonly var elements is (Element of a)[] = bind foreach (v in values) makeElement(v);
    readonly var txs = bind foreach (e in elements) TransformNode { content: bind e.view };

    abstract function render from (index is Integer, value is a) to Node;

    public var selectedValue is a = bind values[selectedRow-scrollOffset];

    var visibleRows is Integer = bind Math.floor(height / cellHeight) as Integer;
    
    override function update to () 
    {
        var y = height/2 - cellHeight/2.0;
        var i = scrollOffset;
        var j = 0;
        while (j < visibleRows and i < model.size) {
            const v = model.getValue(i);
            if (j == values.size()) {
                insert v into values;
            } else {
                values[j] = v;
            }
            elements[j].index = i;
            txs[j].ty = y;
            if (cellHeight > 0) {
                y -= cellHeight;
            } else {
                y -= elements[j].view.contentBounds.height();
            }
            j++;
            i++;
        }
        delete values[j..];
        super.update();
    }

    override var internalContent = Group {
        content: 
        [Rectangle {
                width: bind width;
                height: bind height;
                fillPaint: bind background;
        },
        TransformNode {
            content: bind txs;
        }]
    }

    override function onKeyDown from (e is KeyboardEvent) to ()
    {
	if (e.keyCode == Keys.Down) {
	    selectedRow = Math.min(model.size-1, selectedRow+1);
	} else if (e.keyCode == Keys.Up) {
	    selectedRow = Math.max(0, selectedRow-1);
	} else if (e.keyCode == Keys.Home) {
	    selectedRow = 0;
	} else if (e.keyCode == Keys.End) {
	    selectedRow = model.size-1;
	} else if (e.keyCode == Keys.Enter) {
            apply();
	} else if (e.keyCode == Keys.Esc) {
            cancel();
	}
    }

    // Scrollable

    override public bound function getTx to Number { 0.0 }
    override public bound function getSx to Number { 1.0 }

    override public bound function getTy to Number 
    { 
        scrollOffset / (model.size as Number);
    }

    override public bound function getSy to Number 
    { 
        Math.min(visibleRows / ((model.size) as Number), 1.0);
    }

    override public function setX from (tx is Number, sx is Number) to ()
    {
    }

    override public function setY from (ty is Number, sy is Number) to ()
    {
        scrollOffset = Math.round(ty * model.size) as Integer;
    }

    function scroll from (amount is Integer) to ()
    {
	scrollOffset = Math.max(0, Math.min(scrollOffset + amount, model.size - visibleRows));
    }
    
    const onScroll = internalContent.observeMouseWheel().subscribe(function from (event is MouseWheelEvent) to () {
            const y = event.scroll.y as Integer;
            scroll(y);
    });

    const onClick = 
	internalContent.observeMouseDown().subscribe(function from (e is MouseEvent) to () {
                selectedRow = (scrollOffset + (height-(e.location.y + height / 2)) / cellHeight) as Integer;
                if (e.clickCount > 1) {
                    apply();
                }
	    });

    function apply from () to () 
    {
        applySubject.onNext(selectedValue)
    }

    function cancel from () to () {
        applySubject.onNext(null) /// hack
    }

    var applySubject = Subject of a {};

    public function observeApply to Observable of a {
        applySubject
    }
}
