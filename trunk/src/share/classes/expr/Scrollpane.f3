package expr;
import f3.media.scene.*;

const W = 15.0;
const H = 15.0;

public class Scrollpane is ResizableNode {

    public var content is ResizableNode;

    public var canZoomVertical is Boolean = true;
    public var canZoomHorizontal is Boolean = true;

    function findScrollable {
        var r is Scrollable = null;
        const f is function from ((), Node) to () 
            = function from (_ is (), n is Node) to ()
            {
                if (r == null) {
                    if (n is StageNode) {
                        const stage = n as StageNode;
                        for (scene in stage.content where r == null) {
                            scene.root.foldLeft((), f);
                        }
                    }
                    if (n.visibleInScene and n is Scrollable) {
                        r = n as Scrollable;
                    }
                }
            };
        internalContent.foldLeft((), f);
        if (r <> null) {
            currentScrollable = r;
        } else {
            currentScrollable = null;
        }
    }

    override function resize from (w is Number, h is Number) to ()
    {
        content.resize(w-W, h-H);
        super.resize(w, h);
    }

    override function update {
        findScrollable();
        super.update();
    }

    var currentScrollable is Scrollable;

    override var internalContent = Group {
        content:
        [TransformNode {
                tx: -W/2, ty: H/2
                content: bind content
        },
        TransformNode {
            tx: bind width/2 - W / 2;
            ty: bind H/2
            content: VScrollbar {
                canZoom: bind canZoomVertical;
                width: W;
                height: bind height-H;
                ty: bind currentScrollable.getTy();
                sy: bind currentScrollable.getSy();
                override function setRect from (ty is Number, 
                                                sy is Number) 
                    to ()
                {
                    currentScrollable.setY(ty, sy);
                }
            }
        },
        TransformNode {
            ty: bind -height/2 + H / 2;
            tx: bind -W/2
            content: HScrollbar {
                canZoom: bind canZoomHorizontal;
                height: H;
                width: bind width-W;
                tx: bind currentScrollable.getTx();
                sx: bind currentScrollable.getSx();
                override function setRect from (tx is Number, 
                                                sx is Number) 
                    to ()
                {
                    currentScrollable.setX(tx, sx);
                }
            }
        }]
    }
}