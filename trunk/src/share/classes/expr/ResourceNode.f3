package expr;
import f3.media.scene.*;
import f3.util.Observer.*;
import f3.util.Observable.*;
import f3.util.*;
import expr.prog.Program;

class ResourceChannel {
     public var channel is Channel of Number;
}

class InstanceNode extends TransformNode {
    public var subs is Disposable[];
}

public class ResourceNode is TransformNode, ResourceTarget, NodeAPI {
    public const webBrowserFactory is AbstractWebBrowserFactory = the AbstractWebBrowserFactory;
    public const pathFactory is Shape2D.Path2DFactory = the Shape2D.Path2DFactory;
    public const layoutEngine is Text.LayoutEngine = the Text.LayoutEngine;
    public const effectLoader is CGEffectLoader = the CGEffectLoader;
    public const soundLoader is SoundLoader = the SoundLoader;
    public const movieLoader is MovieLoader = the MovieLoader;
    public const imageLoader is ImageLoader = the ImageLoader;
    override var id = bind url;
    public var temporal is ResourceTimeNode;

    public var collidable is Boolean;
    public var collidableMass is Number = 1.0;
    public var collidableFriction = 0.5;
    public var collidableRestitution = 0.5;
    public var collidableType is RigidBodyType = RigidBodyType.Dynamic;
    public var $dynamicsWorld is DynamicsWorld;
    
    
    var updateSubject = Subject of (()) {};

    public function observeUpdate to Subject of (()) {
        updateSubject;
    }

    override function update to () {
        if (collidable) {
            if (collider == null) {
                collider = $dynamicsWorld.createCollider(this);
            }
            collider.bodyType = collidableType;
            collider.friction = collidableFriction;
            collider.restitution = collidableRestitution;
            collider.mass = collidableMass;
        } else {
            collider.destroy();
            collider = null;
        }
        updateSubject.onNext(());
        super.update();
    }

    override public function findTarget from (id is String) to Object 
    {
        if (id == "") then this else lookup(id);
    }
    
    function subscribe of a from (ob is Observable of a, sourceCode is String, target is Node) to Disposable {
        ob.subscribe(DefaultObserver of a {
                override function onNext from (x is a) to ()
                {
                    println("on next {x} for {sourceCode} of {target}");
                }
            });
    }

    public function instance from (id is String,
                                   lookupVar is function from String to Object,
                                   lookupSpatial is function from String to Node,
                                   lookupTemporal is function from String to TimeNode) to Node 
    {
        const x = this;
        const exprNode = ExprNode {
            url: x.url;
        };
        const target = InstanceNode {
            id: id;
            tx: x.tx;
            ty: x.ty;
            tz: x.tz;
            rx: x.rx;
            ry: x.ry;
            rz: x.rz;
            sx: x.sx;
            sy: x.sy;
            sz: x.sz;
            px: x.px;
            py: x.py;
            pz: x.pz;
            content: exprNode;
        }
        return target;
    }

    public readonly var timeline is Timeline = Timeline {
        content: bind channelBoxes;
        enabled: false;
        paused: true;
    }


    override function createChannels from (parent is ChannelBoxGroup) to (Channel of Number)[]
    {
        const names = ["tx", "ty", "tz", "rx", "ry", "rz", "sx", "sy", "sz", "px", "py", "pz"];
        const ptrs = [&tx, &ty, &tz, &rx, &ry, &rz, &sx, &sy, &sz, &px, &py, &pz];
        foreach (i in [0..<names.size()]) {
            Channel of Number {
                id: bind "{parent.id}#{names[i]}";
                targets: ptrs[i];
            }
        }
    }   
}

public class MaResourceNode is ResourceNode {
    public const model is Ma.Model;
    override var url = bind model.url;
    override var content = bind model.getSpatialRoot();
}

public class F3AssimpResourceNode is ResourceNode {
    public const model is F3Assimp;
    override var url = bind model.url;
    override var content = bind model.getSpatialRoot();
}


public class SoundResourceNode is ResourceNode {

    public const sound is Sound;

    override var content = bind sound;

    function createSoundChannels from (parent is ChannelBoxGroup) to (Channel of Number)[]
    {
        const names = ["volume"];
        const ptrs = [&sound.volume];
        foreach (i in [0..<names.size()]) {
            Channel of Number {
                id: bind "{parent.id}#{names[i]}";
                targets: ptrs[i];
            }
        }
    }   

    var soundChannels is (Channel of Number)[];

    override function createChannels from (parent is ChannelBoxGroup) to (Channel of Number)[] { 
        [createSoundChannels(parent), super.createChannels(parent)];
    }
}


