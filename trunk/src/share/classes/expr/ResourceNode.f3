package expr;
import f3.media.scene.*;
import f3.util.Observer.*;
import f3.util.Observable.*;
import f3.util.*;
import f3.math.*;
import expr.prog.Program;
import expr.InspectedElement.*;

class ResourceChannel {
     public var channel is Channel of Number;
}

class InstanceNode is TransformNode {
    public var subs is Disposable[];
}

public class Button {
    public var pressed is Boolean;
    public var released is Boolean;
    public var held is Boolean;
    public var value is Number;
}

public class Joystick is Button {
    public var x is Number;
    public var y is Number;
    public var angle is Number;
}

class Input {
    public var x is Number;
    public var y is Number;
    public var buttons is Button[];
}

public class ResourceNode is TransformNode, ResourceTarget, NodeAPI, Inspectable {
    public const webBrowserFactory is AbstractWebBrowserFactory = the AbstractWebBrowserFactory;
    public const pathFactory is Shape2D.Path2DFactory = the Shape2D.Path2DFactory;
    public const layoutEngine is Text.LayoutEngine = the Text.LayoutEngine;
    public const effectLoader is CGEffectLoader = the CGEffectLoader;
    public const soundLoader is SoundLoader = the SoundLoader;
    public const movieLoader is MovieLoader = the MovieLoader;
    public const imageLoader is ImageLoader = the ImageLoader;
    override var id = bind url;
    public var temporal is ResourceTimeNode;

    public var collidable is Boolean;
    public var collidableMass is Number = 1.0;
    public var collidableFriction = 0.5;
    public var collidableRestitution = 0.5;
    public var collidableGravityScale is Number = 1.0;
    public var collidableType is RigidBodyType = RigidBodyType.Dynamic;
    public var $dynamicsWorld is DynamicsWorld;

    override var $hideMembers = ["tx", "ty", "tz", "rx", "ry", "rz", "sx", "sy", "sz", "px", "py", "pz", "collidable", "collidableMass", "collidableFriction", "collidableRestitution", "collidableType", "collidableGravityScale", "debug", "id"];

    public const transformGroup = InspectionGroup {
        displayName: "Transform"
        targets:
        [InspectedVector of Number {
                displayName: "Translation"
                targets:[
                         InspectedNumberValue {
                             displayName: "Translate X";
                             target: &tx
                         },
                         InspectedNumberValue {
                             displayName: "Translate Y";
                             target: &ty;
                         },
                         InspectedNumberValue {
                             displayName: "Translate Z";
                             target: &tz;
                         }]
        },
        InspectedVector of Number {
                displayName: "Rotation"
                targets:[
                         InspectedNumberValue {
                             displayName: "Rotate X";
                             target: &rx
                         },
                         InspectedNumberValue {
                             displayName: "Rotate Y";
                             target: &ry;
                         },
                         InspectedNumberValue {
                             displayName: "Rotate Z";
                             target: &rz;
                         }]
        },
        InspectedVector of Number {
                displayName: "Scale"
                targets:[
                         InspectedNumberValue {
                             displayName: "Scale X";
                             target: &sx
                         },
                         InspectedNumberValue {
                             displayName: "Scale Y";
                             target: &sy;
                         },
                         InspectedNumberValue {
                             displayName: "Scale Z";
                             target: &sz;
                         }]
        },
        InspectedVector of Number {
                displayName: "Pivot"
                targets:[
                         InspectedNumberValue {
                             displayName: "Pivot X";
                             target: &px;
                         },
                         InspectedNumberValue {
                             displayName: "Pivot Y";
                             target: &py;
                         },
                         InspectedNumberValue {
                             displayName: "Pivot Z";
                             target: &pz;
                         }]
        }]
    }

    public const collisionGroup is InspectionGroup = InspectionGroup {
        displayName: "Collisions"
        targets:
        [InspectedBooleanValue {
            displayName: "Collidable";
            target: &collidable;
        },
        InspectedValue of RigidBodyType {
            displayName: "Collider Type";
            target: &collidableType;
        },
        InspectedNumberValue {
            displayName: "Mass";
            target: &collidableMass;
        },
        InspectedNumberValue {
            min: 0.0, max: 1.0;
            displayName: "Friction";
            target: &collidableFriction;
        },
        InspectedNumberValue {
            min: 0.0, max: 1.0;
            displayName: "Restitution";
            target: &collidableRestitution;
        },
        InspectedNumberValue {
            min: 0.0, max: 1.0;
            displayName: "Gravity Scale";
            target: &collidableGravityScale;
        }]
    }
    
    
    var updateSubject = Subject of (NodeAPI) {};

    public function observeUpdate to Subject of (NodeAPI) {
        updateSubject;
    }

    function getScene to ResourceScene {
        var p is Node = this;
        while (p <> null) {
            if (p is SceneAPI) {
                return (p as SceneAPI).scene as ResourceScene;
            }
            p = p.parent;
        }
        return null;
    }

    override function update to () {
        if (collidable) {
            if (collider == null) {
                collider = $dynamicsWorld.createCollider(this);
            }
            collider.bodyType = collidableType;
            collider.friction = collidableFriction;
            collider.restitution = collidableRestitution;
            collider.mass = collidableMass;
        } else {
            collider.destroy();
            collider = null;
        }
        if (not getScene().paused) {
            updateSubject.onNext(this);
        }
        super.update();
    }

    override public function findTarget from (id is String) to Object 
    {
        if (id == "") then this else lookup(id);
    }
    
    function subscribe of a from (ob is Observable of a, sourceCode is String, target is Node) to Disposable {
        ob.subscribe(DefaultObserver of a {
                override function onNext from (x is a) to ()
                {
                    println("on next {x} for {sourceCode} of {target}");
                }
            });
    }

    public function instance from (id is String,
                                   lookupVar is function from String to Object,
                                   lookupSpatial is function from String to Node,
                                   lookupTemporal is function from String to TimeNode) to Node 
    {
        const x = this;
        const exprNode = ExprNode {
            url: x.url;
        };
        const target = InstanceNode {
            id: id;
            tx: x.tx;
            ty: x.ty;
            tz: x.tz;
            rx: x.rx;
            ry: x.ry;
            rz: x.rz;
            sx: x.sx;
            sy: x.sy;
            sz: x.sz;
            px: x.px;
            py: x.py;
            pz: x.pz;
            content: exprNode;
        }
        return target;
    }

    public readonly var timeline is Timeline = Timeline {
        content: bind channelBoxes;
        enabled: false;
        paused: true;
    }


    override function createChannels from (parent is ChannelBoxGroup) to (Channel of Number)[]
    {
        const names = ["tx", "ty", "tz", "rx", "ry", "rz", "sx", "sy", "sz", "px", "py", "pz"];
        const ptrs = [&tx, &ty, &tz, &rx, &ry, &rz, &sx, &sy, &sz, &px, &py, &pz];
        foreach (i in [0..<names.size()]) {
            Channel of Number {
                id: bind "{parent.id}#{names[i]}";
                targets: ptrs[i];
            }
        }
    }   

    override function applyForce from (x is Number, y is Number, z is Number) to ()
    {
        collider.applyForce(x, y, z);
    }

    override function applyTorque from (x is Number, y is Number, z is Number) to ()
    {
        collider.applyTorque(x, y, z);
    }
    override function getLinearVelocity from () to Vec3
    {
        collider.getLinearVelocity();
    }
    override function getAngularVelocity from () to Vec3
    {
        collider.getAngularVelocity();
    }
    override function setLinearVelocity from (x is Number, y is Number, z is Number) to () 
    {
        collider.setLinearVelocity(x, y, z)
    }
    override function setAngularVelocity from (x is Number, y is Number, z is Number) to ()
    {
        collider.setAngularVelocity(x, y, z)
    }
    override function applyImpulse from (x is Number, y is Number, z is Number) to () 
    {
        collider.applyImpulse(x, y, z)
    }
    override function applyTorqueImpulse from (x is Number, y is Number, z is Number) to ()
    {
        collider.applyTorqueImpulse(x, y, z);
    }
}

public class MaResourceNode is ResourceNode {
    public const model is Ma.Model;
    override var url = bind model.url;
    override var content = bind model.getSpatialRoot();
}

public class F3AssimpResourceNode is ResourceNode {
    public const model is F3Assimp;
    override var url = bind model.url;
    override var content = bind model.getSpatialRoot();
}


public class SoundResourceNode is ResourceNode {

    public const sound is Sound;

    override var content = bind sound;

    function createSoundChannels from (parent is ChannelBoxGroup) to (Channel of Number)[]
    {
        const names = ["volume"];
        const ptrs = [&sound.volume];
        foreach (i in [0..<names.size()]) {
            Channel of Number {
                id: bind "{parent.id}#{names[i]}";
                targets: ptrs[i];
            }
        }
    }   

    var soundChannels is (Channel of Number)[];

    override function createChannels from (parent is ChannelBoxGroup) to (Channel of Number)[] { 
        [createSoundChannels(parent), super.createChannels(parent)];
    }

}


