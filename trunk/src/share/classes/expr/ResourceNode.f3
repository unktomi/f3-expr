package expr;
import f3.media.scene.*;
import f3.util.Observer.*;
import f3.util.Observable.*;
import f3.util.*;
import expr.prog.Program;

class InstanceNode extends TransformNode {
    public var subs is Disposable[];
}

public class ResourceNode is TransformNode {
    public const webBrowserFactory is AbstractWebBrowserFactory = the AbstractWebBrowserFactory;
    public const pathFactory is Shape2D.Path2DFactory = the Shape2D.Path2DFactory;
    public const layoutEngine is Text.LayoutEngine = the Text.LayoutEngine;
    public const effectLoader is CGEffectLoader = the CGEffectLoader;
    public const soundLoader is SoundLoader = the SoundLoader;
    public const imageLoader is ImageLoader = the ImageLoader;
    public var url is String;
    override var id = bind url;
    public var temporal is ResourceTimeNode;
    public var programs is Program[];

    function subscribe of a from (ob is Observable of a, sourceCode is String, target is Node) to Disposable {
        ob.subscribe(DefaultObserver of a {
                override function onNext from (x is a) to ()
                {
                    println("on next {x} for {sourceCode} of {target}");
                }
            });
    }

    public function instance from (id is String,
                                   lookupVar is function from String to Object,
                                   lookupSpatial is function from String to Node,
                                   lookupTemporal is function from String to TimeNode) to Node 
    {
        const x = this;
        const target = InstanceNode {
            id: id;
            tx: x.tx;
            ty: x.ty;
            tz: x.tz;
            rx: x.rx;
            ry: x.ry;
            rz: x.rz;
            sx: x.sx;
            sy: x.sy;
            sz: x.sz;
            px: x.px;
            py: x.py;
            pz: x.pz;
            content: ExprNode {
                url: x.url;
            }
        }
        function localLookup from (name is String) to Node {
            var n = target.lookup(name);
            if (n == null) {
                n = lookupSpatial(name);
            }
            return n;
        }
        for (prog in programs) {
            if (prog.compiled <> null) {
                const obs = prog.compiled(prog.debugger, target, lookupVar, lookupSpatial, lookupTemporal);
                insert subscribe(obs, prog.sourceCode, target) into target.subs;
            }
        }
        return target;
    }
}

public class MaResourceNode is ResourceNode {
    public const model is Ma.Model;
    override var url = bind model.url;
    override var content = bind model.getSpatialRoot();
}
