package expr.choices;
import f3.media.scene.*;
import f3.media.scene.Shape2D.*;
import f3.media.input.*;
import f3.math.*;
import f3.math.LinearMath.*;
import f3.util.*;
import f3.util.Observer.*;
import java.lang.Math;

function clamp from (n is Number, lo is Number, hi is Number) to Number
{
    Math.min(Math.max(n, lo), hi);
}

const FG = ColorPaint(Color.color(.2, .2, .2));
const BG = ColorPaint(Color.color(.85, .85, .85, 1));

public class SliderField is AbstractNode
{
    public var height is Number = bind textArea.height;
    public var width is Number = 64;

    public var t is Number on replace {
	setText(getText(t));
    }

    postinit {
	setText(getText(t))
    }

    protected function getText from (t is Number) to String
    {
	"{t}"
    }

    public function setText from (text is String) to ()
    {
	textArea.setText(text);
    }

    const textArea is Text.Area = Text.Area {
	foreground: FG;
        background: null;
	width: bind width+4;
	transform: translate(0, 1);
	rightJustified: true;
        font: layoutEngine.createFont("font-size:10;font-family:Lucida Grande;font-weight:bold");
        pickable: false;
    };

    const dark = Color.color(0.77, 0.77, 0.77);
    const light = Color.color(0.85, 0.85, 0.85);
    const border = Color.color(.5, .5, .5);

    const lightRect is Rectangle = Rectangle {
	fillPaint: bind ColorPaint(light);
	height: bind height;
	width: bind width
    }

    const self = this;
    var w = bind runWidth*self.t;

    var dragw is Number;
    var runWidth = bind width + dragw;

    const darkRect is Rectangle = Rectangle {
	fillPaint: bind ColorPaint(dark);
	override var transform = bind translate(-runWidth/2 + self.w/2, 0);
	height: bind height;
	override var width = bind self.w;
        pickable: false;
    }

    const borderRect is Rectangle = Rectangle {
	fillPaint: bind ColorPaint(border);
	override var transform = bind translate(-runWidth/2 + runWidth * self.t - 0.5, 0, 0)
	height: bind height;
	width: 1
    }

    override var internalContent = Group {
        cursor: Cursor.Default;
	content: bind
	[lightRect,
        darkRect,
	textArea,
	borderRect]
    } 

    public function setT from (t is Number) to ()
    {
        this.t = t;
    }     

    const onDrag = DefaultObserver of MouseEvent {
        override function onNext from (event is MouseEvent) to ()
        {     	       		
            setT((event.location.x + width / 2) / width);
         }
    };

    const onUp = DefaultObserver of MouseEvent {
        override function onNext from (event is MouseEvent) to ()
        {     	       		
            onCommit();
        }
    };

    public function onCommit to () {
    }

    var disp1 = lightRect.observeMouseDrag().subscribe(onDrag);
    var disp2 = lightRect.observeMouseUp().subscribe(onUp);
    var disp3 = lightRect.observeMouseDown().subscribe(function from (e is MouseEvent) to ()
                                                       {
                                                           if (e.clickCount > 1) {
                                                           }
                                                       });

    const subject = Subject of Number {};
    public function observeAlpha to Observable of Number { subject }

}